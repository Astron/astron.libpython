// Some abstractions for readability.
typedef uint32 doId;
typedef uint32 zoneId;
typedef uint64 channel;

// The Python Views for these distributed classes.
from example_views import LoginManager/AI/UD
from example_views import DistributedMaproot/AI/UD/AE
from example_views import DistributedAvatar/AI/OV

// A simple DOG for username/password authentication and handoff to
// the DistributedMaproot for avatar creation.
dclass LoginManager {
  login(string username, string password) clsend;
};

// The root object of the map, container for the DistributedAvatars.
dclass DistributedMaproot {
  create_avatar(channel client) airecv;
};

// The actual class for avatar control.
// The idea is that a Client can set its intention for its heading
// and speed, but only the controlling AI can set its actual
// position and heading.
dclass DistributedAvatar {
  setXYZH(int16 / 1000, int16 / 1000, int16 / 1000, int16 / 50) broadcast required;
  indicateIntent(int16 / 10, int16 / 10) ownsend airecv;
};
